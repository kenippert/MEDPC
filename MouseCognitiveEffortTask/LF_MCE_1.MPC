
\cohort 4
\Level 1: STAIRCASING  NP Hold training NO ODOR
\Mouse placed in cage-> house light on for 5 seconds-> nose poke hold for required time ms-> NP LIGHT TURNS ON WHEN HOLD REQ MET
\Enter ANY rewared port for SUCROSE water dispense-> 100 correct or 7200 secodnds deadline
\REWARD DISPENSED UPON ENTERING THE ANY PORT, AND THE

\Nose poke hold set to resolution of 10 ms


\SS1:SET UP STAIRCASING (Z6) AND BLOCKS
\SS2 - ENTRY MONITER AND RECORDS DURATION OF NP HOLDS
\ss3: MAIN LOOP
\SS4: ODOR
\ss5: REWARD
\SS6: ITI
\SS7: SESSION TIMER
\SS8: END OF SESSION BY TIME
\SS10: TONE

\Inputs
^LPORT=1
^np = 2
^RPORT=3
\Outputs
^NPLight =1
^HouseLight=2
^HouseTone=3
^RReward=4
^LREWARD=5
^Odor1=9
^Air=8 \Air flow in to the box
^Vac= 20   \pulls odor out of box

\A= NP HOLD COUNTER
\B= NP LIGHT TIMESTAMP
\C= REWARDS COLLECTED
    \C(1)= APPLE
    \C(2)= REWARDS COLLECTED ON LEVEL 40
\D=  NP DURATION (LIST)
\E= ENTRY TIME STAMP (LIST)
\F= FLAG FOR IN TRIAL (2), OR IN ITI (3)
\G= RESPONSE CATEGORIZATION (1= RIGHT) (2= LEFT PORT) (3= DOUBLE NP) (4= NO COLLECTION)
\H= HOLD TARGET
\I= INDEX (ENTRY INDEX)
\J= NP ENTRY TIMESTAMP
\K=REWARD ENTRY TIMESAMP
\L= LEAVING TIME STAMP
\M= TRIAL OPPORTUNITIES
    \M(1)= TRIAL COUTNER
    \M(2)= INITATIONS
    \M(3)= SUCCESSFUL INITITATIONS
    \M(4)=
\N= List For Staircasing Requirements
\O= LATENCY 1 (NP ENTRY- END OF ITI
\P= PORT IDENTIFIER (1=NP,2= left REWARD PORT, 3= RIGHT REWARD PORT)
\Q= RECORDING IF RESPONSE WAS IN TRIAL (2) OR NOT (1)
\R= Staircasing variable
    \R(1)= TRIAL COUNTER PER BLOCK, GETS RESET AFTER 20 TRIALS IS MET
    \R(2)= TOTAL TRIAL COUNTER (SHOWS CURRENT TRIAL NUMBER)
    \R(3)= STARTING VALUE FOR SUM ARRAY
    \R(4)= VARIABLE TO HOLD THE SUM VALUE OF ARRAY W (SCORE OUT OF 20 FOR  HOLD BLOCKS)
\S=
\T= TIMER
\U= HOLD REQUIREMENT (DRAWN FROM LIST N)
\v= TIMESTAMP FOR LEAVING NP
\W= ARRAY THAT TRACKS IF EACH INITIATION WAS THE REQUIRED HOLD LENGTH
\X= STATS
    \X(1)= AVERAE NP DURATION
    \X(2)= % OF SUCCESSFUL INITIATIONS OUT OF ALL INITIATIONS
    \X(3)= % OF SUCCESSFUL INITIATIONS OUT OF ALL TRIAL OPPORTUNITIES
    \X(4)= % OF TRIALS MOUSE INITIATED ON

\Y= START OF ITI TIMESTAMP (LIST

\z1= LATENCY 1
\Z2= MAIN LOOP (trial starter)
\Z3= lATENCY 2
\Z4= turn on release light
\Z5= REWARD
\z6= Staircasing
\z7= ITI

DIM B= 500
DIM P= 1000
DIM E= 1000
DIM L=1000
DIM D= 1000
DIM O= 500
DIM S= 500
DIM J= 500
DIM G= 500
DIM Q= 1000
DIM Y= 500
DIM C= 5
DIM X= 5
DIM I= 5
DIM M=5
DIM R= 5
DIM K= 500
DIM H= 500
DIM W= 1000
DIM V= 500

LIST N = 10, 20, 40

S.S.1, \SET UP Staircasing -- AFTER EVERY 20 TRIALS, IF THE SUM SCORE OF ACCURATE HOLDS IS 80%, THEN  MOVE TO THE NEXT LEVEL, (IF AT THE TOP LEVEL STAY THERE) IF NOT 80%, HOLD ON CURRENT LEVEL
 S1,
   #START:ON^Houselight; ON^AIR; SET I(2)=0; SET I(1)=0; SET F=3; SET A=0; SET I(3)=0; SET M(1)=0 --->S2
 S2,

   5": OFF^HOUSELIGHT---> S3
s3,
   #K1:SET I(3)= 0;LIST U = N(I(3)) ;Z6---> S4
   #K2: SET I(3)= 1;LIST U = N(I(3)); Z6---> S4
   #K3: SET I(3)=2; LIST U = N(I(3));Z6---> S4
 S4,
   #Z6: IF R(1)>20 [@True, @False]
      @True: SET R(3)=R(2)-10; SUMARRAY R(4)=W,R(3),R(2); IF R(4)>= 8 [@True, @False]
          @True: IF U >=40 [@True, @False]
              @True: SET U = 40; SHOW 3, Current Level, U; Z2 --->S4
              @False: LIST U = N(I(3)) ; SHOW 3, Current Level, U; SET R(1)=1 ; Z2--->S4
          @False: SHOW 3, Current Level, U; Z2 --->S4
      @False: SHOW 3, Current Level, U  ; Z2 ---> S4

S.S.2, \ENTRY MONITER (at any point collectes timestamps and duration)
 S1,
    #START ---> S2
 S2,
    #R^NP: SET P(I(1))=1;  SET E(I(1))=T;   SET Q(I(1))= F; SET H(M(1)) = U--->S4
    #R^RPORT: SET P(I(1))=3; SET E(I(1))=T; SET Q(I(1))= F; ---> S3
    #R^lPORT: SET P(I(1))=2; SET E(I(1))=T; SET Q(I(1))= F; ---> S3
 S3,
    #R^RPORT:---> S3
    #R^lPORT: ---> S3
S4,
   #R^NP: --->S4
   0.01": SET L(I(1))=T; ADD I(1)---> S2

S.S.3 \ MAIN LOOP (and hold requirement met before turning on release light and sending to reward tracking (z5). if hold not long enough send to ITI (z7))
  S1,
    #Z2: SET F=1; set a=0; ADD M(1) ---> S2
  S2,
    #R^NP: ADD R(1);ADD R(2); ADD M(2); SET J(M(1))= T;Z1 ---> S3
 S3, \  HOLD REQUIREMENT
    #R^NP: ADD A; IF A >= U [@TRUE, @FALSE]
       @TRUE: z4  ---> S3
       @FALSE: ---> Sx
    0.01":  If A < U [@TRUE, @False]
            @true: Z7 ; SET W(R(2))= 0 ; SET D(M(1))= A ; SET V(M(1))=T--->s1
            @False:SET W(R(2))= 1; SET D(M(1))= A ; ADD M(3); z5---> s1


S.S.4 \ TURN ON NP LIGHT FOR 1 SEC
s1,
   #z4: ON^NPLight; SET B(M(1))=T  ---> S2
S2,
   1": OFF^NPLight; ---> S1

S.S.5 \REWARD (if mouse is correct, give reward, if wrong DO NOTHING, if they re-enter the NP sent to ITI, if they dont collect in 10 seconds send to ITI)
S1,
   #Z5: SET F=2 ---> S2
S2,
   #R^RPORT:z3; ON^RREWARD; SET K(M(1))=T ; ADD C(1); SET G(M(1))=2; SHOW 6, COLLECTED, c(1); IF U = 40 [@TRUE, @FALSE]
        @TRUE: ADD C(2); Z7 ---> S3
        @FALSE: Z7 ---> S3
   #R^LPORT:z3; ON^RREWARD; SET K(M(1))=T; ADD C(1); SET G(M(1))=1; SHOW 6, COLLECTED, c(1); IF U = 40 [@TRUE, @FALSE]
        @TRUE: ADD C(2); Z7 ---> S3
        @FALSE: Z7 ---> S3
   #R^NP:SET K(M(1))=T; ADD X(1); SET G(M(1))=3; Z7 ---> s1
   10":  SET K(M(1))=T; Z7; SET G(M(1))= 4 ---> S1
S3,
   1": OFF^RREWARD;OFF^LREWARD  ---> S1

S.S.11
S1, \LATENCEY
 #Z3: SET S(M(1))= K(M(1))- V(M(1))---> S1
 #Z1: SET O(M(1))= J(M(1))- ((Y(M(1)-1)+10))---> S1


S.S.6 \  ITI (after ITI calculate if at the end of a block, if so return to scent selection (z1) if not return to beginning of main loop (z2))
 S1,
    #Z7: SET F=3; ON^HOUSELIGHT; SET Y(M(1))=T; ON^VAC; OFF^AIR ---> S2
S2,
   5": OFF^VAC;OFF^HOUSELIGHT; ON^AIR; ADD I(2)---> s3
S3,
  0.01":If C(1) =100[@TRUE,@FALSE]
            @TRUE: IF C(2)>= 20 [@TRUE, @FALSE]
                    @TRUE: ON^HouseLight; OFF^Air; Z9---> S1
                    @FALSE: Z6 ---> S1
           @FALSE : Z6 ---> s1

S.S.7, \ SESSION TIMER
S1,
#START: SET T=0---> S2
S2,
0.01": SET T=T+0.01; SHOW 1, SESSION TIMER, T---> SX

S.S.8, \END OF SESSION BY TIME (end after 1.5 hours, and calculate accuracy)
S1,
#START: ---> S2
S2,
120':Z9 ---> S1


S.S.9, \Checks for activity, stops session if none in 20 minutes
S1,
#START:--->S2
S2,
#R^NP:--->S2
20': Z9 --->S1


S.S.10
S1,
#Z9: SET P(I(1))=-987.987; SET L(I(1))= -987.987; SET E(I(1))= -987.987;
  SET D(I(1))= -987.987; SET Q(I(1))= -987.987; SET G(M(1))= -987.987; SET Y(M(1))= -987.987;
  SET O(I(1))= -987.987;SET J(I(1))= -987.987; SET B(I(1))= -987.987; SET V(M(1))= -987.987;SET O(M(1))= -987.987;SET K(M(1))= -987.987; ---> S2

S2,
0.01": ARITHMETICMEAN X(1) = O,1,500 ; SET X(2)= M(3)/M(2); SET X(3)= M(3)/ M(1); SET X(4)= M(2)/M(1) ---> S3
S3,
10": ---> STOPSAVE

